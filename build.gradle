import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

compileJava {
    options.encoding = 'UTF-8'
}

// SkArclight version
version = '1.2.0'

repositories {
    mavenCentral()
    mavenLocal()

    // Paper
    maven { url 'https://repo.papermc.io/repository/maven-public/' }

    // Skript
    maven { url 'https://repo.skriptlang.org/releases' }
}

dependencies {
    // Paper
    compileOnly("io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT")

    // Skript
    compileOnly(group: 'com.github.SkriptLang', name: 'Skript', version: '2.9.4') {
        transitive = false
    }

    // Logging
    compileOnly("org.apache.logging.log4j:log4j-core:2.20.0")

    // bStats
    implementation(group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.2')
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-source', '' + 11, '-target', '' + 11]
}

build {
    dependsOn(shadowJar)
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.version]
}

shadowJar {
    archiveClassifier = null
    relocate("org.bstats", "com.shanebeestudios.arc.metrics")
}

// Run task to build jar to another spot
tasks.register('server', Copy) {
    from shadowJar
    // Change this to wherever you want your jar to build
    into '/Users/ShaneBee/Desktop/Server/Skript/mohist/plugins/'
}
